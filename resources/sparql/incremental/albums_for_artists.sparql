# Fetch albums for a batch of artist QIDs - optimized for speed
# Used by weekly incremental sync to refresh albums for changed artists

SELECT DISTINCT
  ?album
  ?albumLabel
  ?albumDescription
  ?artist
  ?albumType
  ?publicationDate
  ?musicBrainzReleaseGroupId
WHERE {
  VALUES ?artist { {{values}} }

  ?album wdt:P175 ?artist .

  # Constrain to album-like items (specific types for performance)
  ?album wdt:P31 ?albumType .
  VALUES ?albumType {
    wd:Q482994    # studio album
    wd:Q169930    # EP
    wd:Q134556    # single
    wd:Q222910    # compilation album
    wd:Q209939    # live album
    wd:Q59481898  # remix album
    wd:Q24672043  # soundtrack album
  }

  OPTIONAL { ?album wdt:P577 ?publicationDate . }
  OPTIONAL { ?album wdt:P436 ?musicBrainzReleaseGroupId . }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en,de,fr,es,it,pt,nl,*" .
  }
}
