PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
  ?genre
  ?oid
  ?genreLabel
  ?genreDescription
  ?inception
  ?country ?countryLabel
  ?parentGenre ?parentGenreLabel
  ?musicBrainzId
WHERE {
  ?genre wdt:P31 wd:Q188451 .

  OPTIONAL { ?genre wdt:P571 ?inception . }
  OPTIONAL { ?genre wdt:P495 ?country . }
  OPTIONAL { ?genre wdt:P279 ?parentGenre . }
  OPTIONAL { ?genre wdt:P436 ?musicBrainzId . }

  # Bind numeric O-ID for cursor paging
  BIND(xsd:integer(STRAFTER(STR(?genre), "entity/Q")) AS ?oid)
  {{after_filter}}

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
ORDER BY ?oid
LIMIT {{limit}}
